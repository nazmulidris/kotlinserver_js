/*
 * Copyright 2018 Nazmul Idris All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

group 'com.sample.kotlinserver'
version '1.0-SNAPSHOT'

// More info on Node plugin
// https://github.com/srs/gradle-node-plugin/blob/master/docs/node.md

buildscript {
    ext.kotlin_version = '1.2.10'
    repositories {
        mavenCentral()

        // gradle-node-plugin
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // gradle-node-plugin
        classpath "com.moowork.gradle:gradle-node-plugin:1.2.0"
    }
}

apply plugin: 'kotlin2js'

// gradle-node-plugin
apply plugin: "com.moowork.node"

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
}

compileKotlin2Js {
    kotlinOptions.outputFile = "lib/index.js"
    kotlinOptions.moduleKind = "commonjs"
    kotlinOptions.sourceMap = true
}

// Tasks

// gradle-node-plugin
task runNodeServer(type: NodeTask) {
    script = file('lib/index.js')
    ignoreExitValue = true
}

task stage(dependsOn: ['runNodeServer', 'build', 'npmInstall', 'nodeSetup', 'npmSetup'])
runNodeServer.shouldRunAfter build
build.shouldRunAfter npmInstall
npmInstall.shouldRunAfter nodeSetup
nodeSetup.shouldRunAfter npmSetup